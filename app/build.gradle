apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.abank.skeleton"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildOutputApkName(applicationVariants, "release")
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildOutputApkName(applicationVariants, "debug")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def buildOutputApkName(DomainObjectSet domainObjectSet, String buildType) {
    domainObjectSet.all { variant ->
        variant.outputs.all { output ->
            outputFileName = outputFileName
                    .replace("app-", "")
                    .replace(buildType, "ProjectName-${buildType}_" + new Date().format('dd_MM_yy'))
        }
    }
}

ext {
    supportLib = '1.1.0-alpha04'
    retrofit = '2.3.0'
    room = '2.2.0-alpha01'
    koin = '1.0.2'
    version_anko = '0.10.4'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // support
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.1'
    // retrofit
    implementation "com.squareup.retrofit2:retrofit:${retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofit}"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.13.1'
    // rx
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    // koin
    implementation "org.koin:koin-android-scope:${koin}"
    implementation "org.koin:koin-android-viewmodel:${koin}"
    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.2.0-alpha02'
    // Room
    implementation "androidx.room:room-runtime:${room}"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    kapt "androidx.room:room-compiler:${room}"
    implementation "androidx.room:room-rxjava2:${room}"
    // permissions with dsl
    implementation 'com.github.fondesa:kpermissions:1.0.0'
    // anko
    implementation "org.jetbrains.anko:anko-commons:$version_anko"
    // gson
    implementation 'com.google.code.gson:gson:2.8.5'
    // misc
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.2.1'
    //fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.9@aar') {
        transitive = true
    }
    //mrz
    implementation 'com.google.android.gms:play-services-vision:18.0.0'
    implementation 'com.rmtheis:tess-two:9.0.0'

    //nfc
    implementation "org.jmrtd:jmrtd:0.7.12"
}
